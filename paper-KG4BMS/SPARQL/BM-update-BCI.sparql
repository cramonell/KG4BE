PREFIX iopce: <http://www.w3id.org/iopce#>
PREFIX opm: <http://www.w3id.org/opm#>
PREFIX schema: <https://schema.org/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX bot: <http://www.w3id.org/bot#>
PREFIX BMS: <http://www.example.org/BMS#>

DELETE{
?previousBridgeConditionIndexState a  opm:CurrentPropertyState.
}
INSERT{
?previousBridgeConditionIndexState a  opm:PropertyState.
    
?bridgeConditionIndex opm:hasPropertyState ?currentBridgeConditionIndexState .
?currentBridgeConditionIndexState a  opm:CurrentPropertyState;
    schema:value ?bridgeConditionIndexStateValue ;
prov:generatedAtTime ?evaluationTime .
}WHERE{
{SELECT (MAX(?zoneConditionIndexStateValue) AS ?bridgeConditionIndexStateValue)
WHERE{
    BMS:PR-04-B015 bot:containsZone+ ?zone .
    ?zone iopce:indice_de_condicion ?zoneConditionIndex .
    ?zoneConditionIndex opm:hasPropertyState ?zoneConditionIndexState .
    ?zoneConditionIndexState a opm:CurrentPropertyState ;
        schema:value ?zoneConditionIndexStateValue .
    }
}

# only if there is condition index asociated to the bridge
FILTER EXISTS {BMS:PR-04-B015 iopce:indice_de_condicion  ?bridgeConditionIndex.} 

 # get condition index, condition is the current (and then the previous) condition index state
BMS:PR-04-B015 iopce:indice_de_condicion  ?bridgeConditionIndex . 		
?bridgeConditionIndex opm:hasPropertyState ?previousBridgeConditionIndexState .
?previousBridgeConditionIndexState a  opm:CurrentPropertyState.

# create condition index 
BIND ( NOW() AS ?evaluationTime)
BIND(IRI(CONCAT(str(?bridgeConditionIndex),"-S",str(YEAR(?evaluationTime)), str(MONTH(?evaluationTime)), str(DAY(?evaluationTime)))) AS ?currentBridgeConditionIndexState) 
}